### 力的进化：智能火箭

![图像 2](/Users/yixinxia/Downloads/图像 2.GIF)

我们通过发射火箭模型来实现力的进化

我们火箭有5个推进器，每个推进器的推力大小和方向都是可变的，并且要达到指定目标，中间还要绕开阻碍

使用向量加速度和牛顿第二定律

```
class Rocket {

  PVector location;
  PVector velocity;
  PVector acceleration;
  
   void applyForce2(PVector f) {
    acceleration.add(f);
  }


  void update() {
    velocity.add(acceleration);
    location.add(velocity);
    acceleration.mult(0);
  }
}
```

只需在每一帧中调用applyForce2()并传入一个推力，就能实现智能火箭运动模拟，一旦draw功能被调用，推进器就会对火箭施加一个推进力

遗传算法三个关键

1. 种群规模和突变率
2. 适应度

```
目标：火箭击中靶子
火箭与靶子距离越近适应度越高，反之越低

我们这里假设靶子是一个向量对象
void fitness(){
    float d = PVector.dist(location,target);
    fitness = 1/d;  //适应度与距离成反比
}

我们也可以采用指数级反比
void fitness(){
    float d = PVector.dist(location,target);
    fitness = pow(1/d,2);   //pow功能就是n次方的意思
}
```

3.基因型和表现型

```
推进器会在每一帧产生方向和大小可变的推力
因此我们需要在每一帧动画中获取一个PVector对象
因此基因型用PVector对象数组表示
class DNA{
    PVector[] genes;
}
本案例中基因初始化采用随机PVector对象
PVector v = new PVector(random(-1,1),random(-1,1));
这种所得到的向量在一起形成了一个正方形,由于中心点向正方形边缘的线段长度有长有短
所以我们采用更好的方案
for(int i=0; i< genes.length;i++){
    genes[i] = PVector.random2D();
}
此时向量变成圆形
长度为1的PVector向量代表一个强度很大的力,力改变加速度,加速度会以每秒30次频率改变速度
所以我们要加入一个变量限制PVector对象的长度
class DNA{
    PVector[] = genes;
    float maxforce = 0.1;
    
    DNA(){
        genes = new PVector[lifetime];  //火箭生命期的每一帧对应一个向量对象
        
        for(int i=0; i<genes.length; i++){
            genes[i] = PVector.random2D();
            genes[i].mult(random(0,maxforce));  //用随机方式改变向量长度,但不要超过最大推进力
        }
    }
}
```

我们要rocket对象的作用就是表达PVector数组的遗传信息,也就是表现型

我们只需要添加一个DNA和fitness变量,只有rocket对象知道如何计算它与靶子的距离

因此把适应度放入rocket对象中

```
class Rocket {
    
  DNA dna;  //带入dna变量
  float fitness;    //带入fitness
  int geneCounter =0;   //遍历数组时的计数器

  PVector location;
  PVector velocity;
  PVector acceleration;
  
   void applyForce2(PVector f) {
    acceleration.add(f);
  }

  void update() {
    velocity.add(acceleration);
    location.add(velocity);
    acceleration.mult(0);
  }
  
  void run(){
      applyForce2(dna.genes(geneCounter));  //将基因数组中的力向量作用在火箭上
      geneCounter++;    //转到基因数组的下一个力向量
      update(); //更新火箭属性
  }
}
```

### 整合代码

现在我们有了基因型(DNA对象)和表现型(rocket对象)

我们还需要一个管理火箭系统,类似粒子管理系统,实现选择和繁殖功能

```
class Population{
    float mutationRate; //记录突变绿,种群数组,交配池数组及代计数器的种群变量
    Rocket[] population;
    ArrayList<Rocket>matingPool;
    int generations;    
    
    void fitness(){};   //适应度
    void selection(){};     //选择
    void reproduction(){};  //繁殖
}
```

在本案例中,火箭需要先尝试如何击中靶子,运行一段时间后才能做适应度评估

我们要加入一个新的功能,模拟物理运动,更新所有火箭位置并绘制它们

```
void live(){
    for(int i=0; i<population.length; i++){
        population[i].run();    //run功能负责操纵力,更新火箭位置,显示火箭
    }
}
```

按照顺序调用population,执行遗传算法的每个步骤

```
population.fitness();   //适应度
population.selection();     //选择
population.reproduction();  //繁殖
```

正确的执行步骤如下

1. 创建火箭种群
2. 让所有火箭运行N帧
3. 进化出下一代:选择,繁殖
4. 回到步骤二

主程序如下

```
int lifetime;  //每一代生命期的帧数
int lifeCounter;  //位于哪一帧
Population population;  //种群对象

void setup(){
  size(666,666);
  
  lifetime = 500;
  lifeCounter = 0;
  
  float mutationRate =0.01;
  population = new Population(mutationRate,50);   //第一步:创建种群,我们这里指定突变率和种群规模
}

void draw(){
  background(233);
  
  if(lifeCounter < lifetime) {  //修改后的遗传算法
    population.live();  //第二步:只要小于lifetime,火箭一直存活
    lifeCounter++;
  }else{
    lifeCounter = 0;  //一旦生命期结束,重置lifeCounter,开始下一轮进化(选择和繁殖)
    
    population.fitness();   //适应度
    population.selection(); //选择
    population.reproduction();  //繁殖
  }
  
}
```

截止到目前为止我们的火箭可以经过多次试验后,最终会到达靶子位置

完整版

基因型DNA对象

```
//1.交叉功能
//2.变异功能
//3.新基因代替老基因

class DNA {

  PVector[] genes;
  float maxforce = 0.1;

  DNA() {
    genes = new PVector[lifetime];  //火箭生命期的每一帧对应一个向量对象
    
    for (int i = 0; i < genes.length; i++) {
      genes[i] = new PVector(cos(random(TWO_PI)), sin(random(TWO_PI)));
      genes[i].mult(random(0, maxforce)); //用随机方式改变向量长度,但不要超过最大推进力
    }
  }

  
//1.交叉功能
  DNA crossover(DNA partner) {  //交叉
    PVector[] child = new PVector[genes.length];    //下一代基因数量设置
    int crossover = int(random(genes.length));      //从基因中随机提取1个
    for (int i = 0; i < genes.length; i++) {        
      if (i > crossover) child[i] = genes[i];       
      else               child[i] = partner.genes[i];
    }    
    DNA newgenes = new DNA(child);
    return newgenes;
  }

//2.变异功能    
  void mutate(float m) {    //变异
    for (int i = 0; i < genes.length; i++) {
      if (random(1) < m) {
        genes[i] = new PVector(cos(random(TWO_PI)), sin(random(TWO_PI)));
        genes[i].mult(random(0, maxforce));
      }
    }
  }
  
//3.新基因代替老基因  
  DNA(PVector[] newgenes) {
    genes = newgenes;
  }
}
```

表现型火箭对象

```
//1.牛顿第二定律
//2.向量加速度
//3.适应度
//4.火箭外观
//5.火箭坐标与靶子坐标距离检测
//6.火箭坐标与靶子坐标距离不在指定范围内不断地重复

class Rocket {

  PVector location;
  PVector velocity;
  PVector acceleration;
  float r;
  float fitness;
  DNA dna;
  int geneCounter = 0;
  boolean hitTarget = false;   
  
  Rocket(PVector l, DNA dna_) {
    acceleration = new PVector();
    velocity = new PVector();
    location = l.get();
    r = 4;
    dna = dna_;
  }
  
//1.牛顿第二定律
  void applyForce2(PVector f) {
    acceleration.add(f);
  }
  
//2.向量加速度
  void update() {
    velocity.add(acceleration);
    location.add(velocity);
    acceleration.mult(0);
  }
  
//3.适应度  
  void fitness() {
    float d = dist(location.x, location.y, target.x, target.y);
    fitness = pow(1/d, 2);
  }
  
//4.火箭外观
  void display() {
    float theta = velocity.heading2D() + PI/2;
    fill(200, 100);
    stroke(0);
    pushMatrix();
        translate(location.x, location.y);
        rotate(theta);

        rectMode(CENTER);
        fill(0);
        rect(-r/2, r*2, r/2, r);
        rect(r/2, r*2, r/2, r);

        fill(175);
        beginShape(TRIANGLES);
            vertex(0, -r*2);
            vertex(-r, r*2);
            vertex(r, r*2);
        endShape();

    popMatrix();
  }
  
//5.火箭坐标与靶子坐标距离检测  
  void checkTarget() {
    float d = dist(location.x, location.y, target.x, target.y);
    if (d < 12) {
      hitTarget = true;
    } 
  }
  
//6.火箭坐标与靶子坐标距离不在指定范围内不断地重复尝试  
  void run() {
    checkTarget(); 
    if (!hitTarget) {
      applyForce2(dna.genes[geneCounter]);
      geneCounter = (geneCounter + 1) % dna.genes.length;
      update();
    }
    display();
  }

  float getFitness() {
    return fitness;
  }

  DNA getDNA() {
    return dna;
  }

}
```

表现型 火箭管理系统population

```
//1.模拟物理运动
//2.适应度
//3.选择
//4.繁殖
//5.获取最大适应度

class Population {

  float mutationRate;          // 计数器
  Rocket[] population;         // 火箭数组
  ArrayList<Rocket> matingPool;    // 火箭交配池
  int generations;             // 迭代


   Population(float m, int num) {
    mutationRate = m;
    population = new Rocket[num];
    matingPool = new ArrayList<Rocket>();
    generations = 0;

    for (int i = 0; i < population.length; i++) {
      PVector location = new PVector(width/2,height+20);
      population[i] = new Rocket(location, new DNA());
    }
  }
  
//1.模拟物理运动
  void live () {
    for (int i = 0; i < population.length; i++) {
      population[i].run();
    }
  }
  
//2.适应度
  void fitness() {  //适应度
    for (int i = 0; i < population.length; i++) {
      population[i].fitness();
    }
  }

//3.选择
  void selection() {    //选择
    matingPool.clear();
    float maxFitness = getMaxFitness();

    for (int i = 0; i < population.length; i++) {
      float fitnessNormal = map(population[i].getFitness(),0,maxFitness,0,1);
      int n = (int) (fitnessNormal * 100);  // Arbitrary multiplier
      for (int j = 0; j < n; j++) {
        matingPool.add(population[i]);
      }
    }
  }
  
//4.繁殖
  void reproduction() { //繁殖
    for (int i = 0; i < population.length; i++) {

      int m = int(random(matingPool.size()));
      int d = int(random(matingPool.size()));
      Rocket mom = matingPool.get(m);
      Rocket dad = matingPool.get(d);
      DNA momgenes = mom.getDNA();
      DNA dadgenes = dad.getDNA();
      DNA child = momgenes.crossover(dadgenes);
      child.mutate(mutationRate);
      PVector location = new PVector(width/2,height+20);
      population[i] = new Rocket(location, child);
    }
    generations++;
  }
  
//5.获取最大适应度
  float getMaxFitness() {
    float record = 0;
    for (int i = 0; i < population.length; i++) {
       if(population[i].getFitness() > record) {
         record = population[i].getFitness();
       }
    }
    return record;
  }
  
  int getGenerations() {
    return generations;
  }

}
```

主程序

```
int lifetime;  
Population population;  
int lifeCounter;          
PVector target;  

void setup() {
  size(800, 200);
  smooth();

  lifetime = 200;  //生命期
  lifeCounter = 0;  
  target = new PVector(width/2, 24);    //最终目标靶子位置
  population = new Population(0.01, 50);    //变异概率0.01, 50个火箭

}

void draw() {
  background(255);

  fill(0);
  ellipse(target.x,target.y,24,24);

  if (lifeCounter < lifetime) {
    population.live();  //火箭模拟物理运动
    lifeCounter++;
  } 
  else {
    lifeCounter = 0;
    population.fitness();   //火箭适应度
    population.selection(); //火箭选择
    population.reproduction();  //火箭繁殖
  }

  fill(0);
  text("child #: " + population.getGenerations(), 10, 18);
  text("round: " + (lifetime-lifeCounter), 10, 36);
}

void mousePressed() {   //鼠标位置为靶子位置
  target.x = mouseX;
  target.y = mouseY;
}
```